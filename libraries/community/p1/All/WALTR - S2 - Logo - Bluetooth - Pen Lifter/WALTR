TO NEWFD :DIST
  IF :DIST < 0 [BK -:DIST STOP]
  IF :SCREEN [ORIGFD :DIST]
  LOCAL "DIST2
  MAKE "DIST2 :DIST * :DSCALE
  IF AND :FLOOR :DIST2 < 65536 [SETWRITE :SLOT TYPE (WORD "F CHAR INT :DIST2 / 256 CHAR REMAINDER :DIST2 256 CHAR 13) SETWRITE []]
END

TO NEWBK :DIST
  IF :DIST < 0 [FD -:DIST STOP]
  IF :SCREEN [ORIGBK :DIST]
  LOCAL "DIST2
  MAKE "DIST2 :DIST * :DSCALE
  IF AND :FLOOR :DIST2 < 65536 [SETWRITE :SLOT TYPE (WORD "B CHAR INT :DIST2 / 256 CHAR REMAINDER :DIST2 256 CHAR 13) SETWRITE []]
END

TO NEWLT :ANGL
  IF :ANGL < 0 [RT -:ANGL STOP]
  IF :SCREEN [ORIGLT :ANGL]
  IF AND :FLOOR :ANGL < 65536 [SETWRITE :SLOT TYPE (WORD "L CHAR INT :ANGL / 256 CHAR REMAINDER :ANGL 256 CHAR 13) SETWRITE []]
END

TO NEWRT :ANGL
  IF :ANGL < 0 [LT -:ANGL STOP]
  IF :SCREEN [ORIGRT :ANGL]
  IF AND :FLOOR :ANGL < 65536 [SETWRITE :SLOT TYPE (WORD "R CHAR INT :ANGL / 256 CHAR REMAINDER :ANGL 256 CHAR 13) SETWRITE []]
END

TO NEWPU 
  IF :SCREEN [ORIGPU]
  IF :FLOOR [SETWRITE :SLOT TYPE WORD "PU CHAR 13 SETWRITE []]
END

TO NEWPD 
  IF :SCREEN [ORIGPD]
  IF :FLOOR [SETWRITE :SLOT TYPE WORD "PD CHAR 13 SETWRITE []]
END

TO MODELOGO 
  IF :FLOOR [SETWRITE :SLOT TYPE WORD "1 CHAR 13 SETWRITE []]
END

TO MODEDIRECT 
  IF :FLOOR [SETWRITE :SLOT TYPE WORD "2 CHAR 13 SETWRITE []]
END

TO SPEED :SPEED 
  IF :FLOOR [SETWRITE :SLOT TYPE (WORD "V CHAR :SPEED CHAR 13) SETWRITE []]
END

TO BACKUP :LIST
  IF EMPTYP :LIST [STOP]
  COPYDEF FIRST :LIST WORD "ORIG FIRST :LIST
  BACKUP BF :LIST
END

TO LCOPYDEF :LIST
  IF EMPTYP :LIST [STOP]
  COPYDEF FIRST BF :LIST FIRST :LIST
  LCOPYDEF BF BF :LIST
END

TO WALTRINIT 
  MAKE "REDEFP "TRUE
  BACKUP [FD BK RT LT PU PD]
  LCOPYDEF [FD NEWFD BK NEWBK RT NEWRT LT NEWLT PU NEWPU PD NEWPD]
  ERASE [NEWFD NEWBK NEWRT NEWLT NEWPU NEWPD]
  LCOPYDEF [FORWARD FD BACK BK RIGHT RT LEFT LT PENUP PU PENDOWN PD]
  MAKE "REDEFP "FALSE
  OPEN :SLOT
  ERASE [WALTRINIT]
END

TO WALTREXIT 
  CLOSE :SLOT
  MAKE "REDEFP "TRUE
  LCOPYDEF [FD ORIGFD BK ORIGBK RT ORIGRT LT ORIGLT PU ORIGPU PD ORIGPD]
  LCOPYDEF [FORWARD FD BACK BK RIGHT RT LEFT LT PENUP PU PENDOWN PD]
  ERASE [ORIGFD ORIGBK ORIGRT ORIGLT ORIGPU ORIGPD]
  MAKE "REDEFP "FALSE
  ERASE [WALTREXIT BACKUP LCOPYDEF]
END

MAKE "FLOOR "TRUE
MAKE "SCREEN "TRUE
MAKE "DSCALE 5
MAKE "SLOT 1

WALTRINIT
