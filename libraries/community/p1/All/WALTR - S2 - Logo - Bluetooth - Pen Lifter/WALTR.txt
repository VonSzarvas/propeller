10 D$ = CHR$(4)
20 FDIR = 0:RDIR = 0
30 SPD = 1:WSPD = 50
40 BUP = 0:BDN = 0:B0 = 0:B1 = 0
50 DIM SL(6)
60 SL(0) = 0:SL(1) = 30:SL(2) = 50:SL(3) = 70:SL(4) = 90:SL(5) = 100:SL(6) = 120
70 PRINT D$;"PR#1"
80 PRINT "2"; CHR$(13);"S";: REM  SET TO DIRECT MODE AND PEN UP
90 PRINT "B"; CHR$(WSPD);"G"; CHR$(WSPD);
100 F =  PDL(1): FOR I = 1 TO 2: NEXT :R =  PDL(0): FOR I = 1 TO 5: NEXT :B1 =  PEEK(49250):B0 =  PEEK(49249)
110 IF B1 > 127 AND BUP = 0 THEN BUP = 1: IF SPD <= 5 THEN SPD = SPD + 1:WSPD = SL(SPD): PRINT "B"; CHR$(WSPD);"G"; CHR$(WSPD);
120 IF B1 <= 127 AND BUP <> 0 THEN BUP = 0
130 IF B0 > 127 AND BDN = 0 THEN BDN = 1: IF SPD > 2 THEN SPD = SPD - 1:WSPD = SL(SPD): PRINT "B"; CHR$(WSPD);"G"; CHR$(WSPD);
140 IF B0 <= 127 AND BDN <> 0 THEN BDN = 0
150 IF F < 85 THEN FDIR =  - 1
160 IF F >= 85 AND F <= 170 THEN FDIR = 0
170 IF F > 170 THEN FDIR = 1
180 IF R < 85 THEN RDIR =  - 1
190 IF R >= 85 AND R <= 170 THEN RDIR = 0
200 IF R > 170 THEN RDIR = 1
210 IF RDIR < 0 THEN PRINT ",";
220 IF RDIR > 0 THEN PRINT ".";
230 IF RDIR = 0 AND FDIR < 0 THEN PRINT "A";
240 IF RDIR = 0 AND FDIR > 0 THEN PRINT "Z";
250 IF RDIR = 0 AND FDIR = 0 THEN PRINT " ";
260 IF PEEK(- 16384) < 127 THEN GOTO 100: REM  EXIT LOOP ON KEY PRESS
270 PRINT D$;"PR#0"
