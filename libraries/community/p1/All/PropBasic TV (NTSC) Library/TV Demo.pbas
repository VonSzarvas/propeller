' This demo program moves 1000 dots around on a NTSC screen.
'
DEVICE P8X32A, XTAL1, PLL16X
FREQ 80_000_000

LOAD "TV.lib"

' Define CONs
NumDots   CON 1000
MaxDots   CON 999


' Define HUB variables
dots    HUB LONG(NumDots) ' x, y, xdir, ydir


' Define variables (LONGs only)
curDot     VAR LONG
dotsAddr   VAR LONG
X          VAR LONG
Y          VAR LONG
dirX       VAR LONG
dirY       VAR LONG
seed       VAR LONG
temp       VAR LONG


' Start of main code
PROGRAM Start 

Start:
  dotsAddr = GETADDR dots
  FOR curDot = 0 TO MaxDots
    RANDOM seed, x
    x = x AND 255
    DO
      RANDOM seed, y
      y = y AND 255
    LOOP UNTIL y < 192
    DO
      RANDOM seed, dirX
      dirX = dirX AND 255
      dirX = dirX / 52
      RANDOM seed, dirY
      dirY = dirY AND 255
      dirY = dirY / 52
      IF dirX <> 2 OR
       dirY <> 2 THEN EXIT
    LOOP  
    WRBYTE dotsAddr, x, y, dirX, dirY
    TV_XPlot x, y
    dotsAddr = dotsAddr + 4
  NEXT

  DO
    dotsAddr = GETADDR dots
    FOR curDot = 0 TO MaxDots
      RDBYTE dotsAddr, x, y, dirX, dirY
      TV_XPlot x, y
      x =x + dirX
      x = x - 2
      IF x > 255 OR
       x < 0 THEN
        dirX = 4 - dirX
        x = x + dirX
        x = x - 2
      ENDIF

      y = y + dirY
      y = y - 2
      IF y > 191 OR
       y < 0 THEN
        dirY = 4 - dirY
        y = y + dirY
        y = y - 2
      ENDIF

      TV_XPlot x, y

      WRBYTE dotsAddr, x, y, dirX, dirY

      dotsAddr = dotsAddr + 4
    NEXT

    PAUSE 10

  LOOP
END

{
!------------------------------------------------------------------------------------------------------------------------------
!                                                   TERMS OF USE: MIT License                                                 !
!-----------------------------------------------------------------------------------------------------------------------------!
!Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation   !
!files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,   !
!modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the        !
!Software is furnished to do so, subject to the following conditions:                                                         !
!                                                                                                                             !
!The above copyright notice and this permission notice shall be included in all copies or substantial portions of the         !
!Software.                                                                                                                    !
!                                                                                                                             !
!THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE         !
!WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR        !
!COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,  !
!ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                        !
-------------------------------------------------------------------------------------------------------------------------------
}

